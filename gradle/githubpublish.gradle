apply plugin: 'maven-publish'

def releaseGroupId = 'sg.com.sph.android'
def releaseArtifactId = "${project.name}".toLowerCase()
def versionName = android.defaultConfig.versionName.toString()

task sourceJar(type: Jar) {
    classifier "sources"
    from android.sourceSets.main.java.srcDirs
}

publishing {
    publications {
        aar(MavenPublication) {
            groupId releaseGroupId
            artifactId releaseArtifactId
            version versionName

            artifact("$buildDir/outputs/aar/$releaseArtifactId-release.aar")
            artifact(sourceJar)

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                //Iterate over the compile dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.implementation.allDependencies.each {
                    if (it.name != 'unspecified' && it.group != null && it.version != null &&
                            it.version != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
                configurations.api.allDependencies.each {
                    if (it.name != 'unspecified' && it.group != null && it.version != null &&
                            it.version != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/SPHTech/mobile-android")
            credentials {
                def localProperties = new Properties()
                File localPropFile = rootProject.file("local.properties")
                if (localPropFile.exists()) {
                    localPropFile.withInputStream { localProperties.load(it) }
                }
                username = System.getenv("GITHUB_ACTOR") ?: localProperties["GITHUB_USERNAME"]
                password = System.getenv("GITHUB_TOKEN") ?: localProperties["GITHUB_TOKEN"]
            }
        }
    }
}
